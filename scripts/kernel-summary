#!/usr/bin/perl -w
# Keep track of Linux kernel versions and ABI bumps
use strict;

use File::Slurp;
use Email::MIME;
use Email::Sender::Simple qw(sendmail);
use List::MoreUtils qw(uniq);
use Sort::Versions;
use LWP::UserAgent;


sub colorize_flavours {
  my ($arch, $abi, $lastabi, %v) = @_;
  my $cell;
  if ($v{$arch}{$abi}) {
    foreach my $flavour (@{$v{$arch}{$abi}}) {
      my $color;
      if (($abi ne $lastabi) && $v{$arch}{$lastabi}) {
        if (grep { $_ eq $flavour } @{$v{$arch}{$lastabi}}) {
          # Available in a newer ABI, can be decrufted:
          $color='darkorange';
        }
        else {
          # Not available in newer ABI, possibly dropped:
          $color='purple';
        }
      }
      if (($abi ne $lastabi) && !$v{$arch}{$lastabi}) {
        # Not built yet:
        $color='red';
      }
      if ($color) {
        $cell .= "<span style=\"color: $color;\">$flavour</span><br/>";
      }
      else {
        $cell .= "$flavour<br/>";
      }
    }
  }
  $cell ||= "&nbsp;";
  return "<td>$cell</td>\n";
}

sub compare_and_mail {
  my ($dist, $lastabi, $summary, $url) = @_;
  if (-f $summary) {
    foreach my $line (read_file($summary)) {
      if ($line =~ /<!-- ABI: (.*) in (.*) -->/) {
        my $previous_lastabi = $1;
        my $previous_dist = $2;
        if (($dist eq $previous_dist) and ($lastabi ne $previous_lastabi)) {
          my $subject = "Linux kernel ABI bump in $dist: from $previous_lastabi to $lastabi";
          my $body = "Linux kernel ABI bump in $dist: from $previous_lastabi to $lastabi\n\nFull summary: $url#$dist\n";
          my $message = Email::MIME->create(
            header_str => [
              From    => 'Linux kernel watcher <debian-boot@lists.debian.org>',
              To      => 'debian-boot@lists.debian.org',
              Subject => $subject,
            ],
            attributes => {
              encoding => "quoted-printable",
              charset  => "UTF-8",
            },
            body_str => $body,
          );
          sendmail($message);
          print STDERR "Sending a notification: $subject\n";
        }
      }
    }
  }
  # Beware: keep in sync with the match above
  return "<!-- ABI: $lastabi in $dist -->";
}

sub build_status {
  my ($dist, @allarchs) = @_;
  my $buildurl = "https://buildd.debian.org/status/package.php?p=linux&suite=$dist";

  # Only look at build logs for those distributions:
  return ''
    if ! grep { $dist eq $_ } qw(unstable experimental);

  my $html;
  my $ua = LWP::UserAgent->new;
  my $response = $ua->get($buildurl);
  my %builds;
  if ($response->is_success) {
    foreach my $line (split /\n/, $response->content) {
      # FIXME: The above match is fragile, but that'll do for now:
      if ($line =~ m{<tr>.*<td>.*<a href="[^"]+">(.+?)</a>.*?</td><td.*?class="status ([^"]+)".*?</td><td>(.+?)</td>}) {
        my ($arch, $status, $time) = ($1, $2, $3);
        # Adjust status and precision:
        $status =~ s/^status-//;
        $time =~ s/\s+\d+m$//;
        my %color = ( installed => 'darkgreen', building => 'darkorange', buildattempted => 'red'   );
        my %label = ( installed => 'Installed', building => 'Building'  , buildattempted => 'Failed');
        my $c = $color{$status} || 'black';
        my $l = $label{$status} || $status;
        $builds{$arch} = "<span style=\"color: $c\">$l ($time)</span>";
      }
    }
    $html .= "<tr>\n";
    $html .= "<th>Build</th>\n";
    foreach my $arch (@allarchs) {
      $html .= "<td>$builds{$arch}</td>\n";
    }
    $html .= "</tr>\n";
  }
  else {
    my $colspan = 1+scalar(@allarchs);
    $html .= "<tr><td colspan=\"$colspan\">Failed to retrieve build status: " . $response->status_line . "</td></tr>";
  }
  return $html;
}


my $summary = shift @ARGV || "kernel-summary.html";
my $url = "http://d-i.debian.org/kernel-summary.html";

my $html = "<html><head><title>Linux kernel status across distributions</title></head><body>\n";
foreach my $dist (qw(testing unstable experimental)) {
  # FIXME: A few hours can happen until a built package can be seen
  # here. Another option would be ssh {ftp-master,release}.d.o dak ls
  my @lines = `rmadison -S linux -s $dist|grep linux-image-`;
  my @allabis;
  my @allarchs;
  my %v;
  foreach my $line (@lines) {
    chomp $line;
    my ($package, $version, $dist, $archs) = split /\s*[|]\s*/, $line;
    $package =~ s/\s*linux-image-//;
    (my $abi = $package) =~ s/(-[\d+])-.*/$1/;
    (my $flavour = $package) =~ s/.*?-[\d+]-//;
    #print "$abi; $package; $version; $dist; $archs\n";
    foreach my $arch (split /,\s*/, $archs) {
      push @{$v{$arch}{$abi}}, $flavour;
      push @allarchs, $arch;
    }
    push @allabis, $abi;
  }

  ### Sort and uniquify:
  @allarchs = uniq sort @allarchs;
  @allabis = uniq sort { versioncmp($a,$b) } @allabis;
  my $none = "(none)";
  my $lastabi = $allabis[-1] || $none;

  ### Compare with previous ABI if relevant:
  my $abi_comment = compare_and_mail($dist, $lastabi, $summary, $url);

  ### Output:
  $html .= "<a name=\"$dist\"><h1>Linux kernel status in $dist</h1></a>\n";
  $html .= "$abi_comment\n";
  $html .= "<p>Top-most ABI: $lastabi</p>\n";
  if ($lastabi ne $none) {
    $html .= "<table border=1 cellpadding=3 style=\"border-collapse: collapse;\">\n";
    # First row, with archs:
    $html .= "<tr>\n";
    $html .= "<th></th>\n";
    foreach my $arch (@allarchs) {
      $html .= "<th>$arch</th>\n";
    }
    $html .= "</tr>\n";
    # Actual contents:
    foreach my $abi (@allabis) {
      $html .= "<tr>\n";
      $html .= "<th>$abi</th>\n";
      foreach my $arch (@allarchs) {
        $html .= colorize_flavours($arch, $abi, $lastabi, %v);
      }
      $html .= "</tr>\n";
    }
    $html .= build_status($dist, @allarchs);
    $html .= "</table>\n";
  }
}


my $date = `LC_ALL=C TZ=UTC date`;
chomp $date;
$html .= "<hr/>\n";
$html .= "Last update: $date\n";
$html .= "</body></html>\n";

write_file($summary, $html);
